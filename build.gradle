plugins {
    id 'java'
    id 'io.qameta.allure' version '3.0.1'
}

group = 'ru.vasiliy'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

def passedProps = ['env', 'browser', 'browserVersion', 'browserSize', 'baseUrl', 'remote', 'videoUrlPattern']

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperty 'file.encoding', 'UTF-8'

    passedProps.each { p ->
        def v = System.getProperty(p)
        if (v != null) systemProperty p, v
    }
}

def allureVersion = "2.27.0"
def selenideVersion = "7.5.1"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            'org.junit.jupiter:junit-jupiter:5.10.2',
            'com.codeborne:selenide:7.7.1',
            'io.qameta.allure:allure-selenide:2.27.0',
            'io.rest-assured:rest-assured:5.5.0',
            'io.qameta.allure:allure-rest-assured:2.27.0',
            'com.fasterxml.jackson.core:jackson-databind:2.17.2',
            'io.appium:java-client:9.2.2',
            'org.aeonbits.owner:owner:1.0.12',
            'io.qameta.allure:allure-junit5:2.27.0',
            'org.assertj:assertj-core:3.26.3',
            'org.slf4j:slf4j-simple:2.0.13',
            'com.github.javafaker:javafaker:1.0.2'
    )
}

tasks.register('webTest', Test) {
    useJUnitPlatform()
    testLogging {
        lifecycle {
            events "passed", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "full"
        }
    }
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    include('**/web/**')
}

tasks.register('apiTest', Test) {
    useJUnitPlatform()
    testLogging {
        lifecycle {
            events "passed", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "full"
        }
    }
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    include('**/api/**')
}









